/*******************************************************************************
 * Copyright (c) 2016-2017 Istvan David
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Istvan David - initial API and implementation
 *******************************************************************************/

package be.uantwerpen.msdl.icm.queries.inconsistencies

import "metamodels.pm"
import "metamodels.properties"
import "metamodels.processmodel"

import pattern be.uantwerpen.msdl.icm.queries.general.nodeGloballyReachableFromNode
import pattern be.uantwerpen.msdl.icm.queries.general.intent
import be.uantwerpen.msdl.icm.queries.general.propertyGloballyReachableFromProperty
import be.uantwerpen.msdl.icm.queries.general.parallelActivities2

pattern sharedProperty(activity1: Activity, intent1: Intent, property1: Property, activity2: Activity, intent2: Intent, property2: Property){
	activity1!=activity2;
	find intent(activity1, property1, intent1);
	find intent(activity2, property2, intent2);
	find propertyGloballyReachableFromProperty(property1, property2);
}

pattern sequentialSharedProperty(activity1: Activity, intent1: Intent, property1: Property, activity2: Activity, intent2: Intent, property2: Property){
	find sharedProperty(activity1, intent1, property1, activity2, intent2, property2);
	find nodeGloballyReachableFromNode(activity2, activity1);
}

pattern parallelSharedProperty(activity1: Activity, intent1: Intent, property1: Property, activity2: Activity, intent2: Intent, property2: Property, fork: Fork){
	find sharedProperty(activity1, intent1, property1, activity2, intent2, property2);
	find parallelActivities2(activity1, activity2, fork, _);
}

pattern readModifySharedProperty(activity1: Activity, property1: Property, activity2: Activity, property2: Property){
	find sequentialSharedProperty(activity1, intent1, property1, activity2, intent2, property2);
	Intent.type(intent1, IntentType::READ);
	Intent.type(intent2, IntentType::MODIFY);
}

pattern modifyModifySharedPropertySequential(activity1: Activity, property1: Property, activity2: Activity, property2: Property){
	find sequentialSharedProperty(activity1, intent1, property1, activity2, intent2, property2);
	Intent.type(intent1, IntentType::MODIFY);
	Intent.type(intent2, IntentType::MODIFY);
}

pattern modifyModifySharedPropertyParallel(activity1: Activity, property1: Property, activity2: Activity, property2: Property, fork: Fork){
	find parallelSharedProperty(activity1, intent1, property1, activity2, intent2, property2, fork);
	Intent.type(intent1, IntentType::MODIFY);
	Intent.type(intent2, IntentType::MODIFY);
}

pattern forkOutControl(forkNode : Fork){
	Fork.controlOut(forkNode,_);
}